alias funNum R1;
alias currPID R2;

if(funNum==ACQUIRE_TERMINAL)then

    while([TERMINAL_STATUS_TABLE]==1)do
        [PROCESS_TABLE + currPID*16 + 4]=WAIT_TERMINAL;
        multipush(R1, R2);
        call MOD_5;
        multipop(R1, R2); 
    endwhile;

    [TERMINAL_STATUS_TABLE]=1;
    [TERMINAL_STATUS_TABLE + 1]=currPID;

    return;
endif;


if(funNum==RELEASE_TERMINAL)then 

    if(currPID!=[TERMINAL_STATUS_TABLE+1])then   
        R0 = -1;
        return;
    endif;

    [TERMINAL_STATUS_TABLE]=0;

    alias count R6;
    count = 0;
    while(count<16)do   
        if([PROCESS_TABLE + count*16 + 4]==WAIT_TERMINAL)then   
            [PROCESS_TABLE + count*16 + 4]=READY;
        endif;
        count = count + 1;
    endwhile;

    R0 = 0;
    return;
endif;


if(funNum==ACQUIRE_DISK)then

    while([DISK_STATUS_TABLE]==1)do
        [PROCESS_TABLE + currPID*16 + 4]=WAIT_DISK;
        multipush(R1, R2);
        call MOD_5;
        multipop(R1, R2); 
    endwhile;

    [DISK_STATUS_TABLE]=1;
    [DISK_STATUS_TABLE + 4]=currPID;

    return;
endif;


if(funNum == ACQUIRE_SEMAPHORE)then
    alias index R0;
    R0 = 0;

    while(index < MAX_SEM_COUNT)do                       
        if([SEMAPHORE_TABLE + index*4 + 1] == 0)then
            [SEMAPHORE_TABLE + index*4 + 1] = 1;
            [SEMAPHORE_TABLE + index*4 + 0] = -1;
            return;
        endif;
        index = index + 1;        
    endwhile;

    index = -1;                                          

    return;
endif;


if(funNum == RELEASE_SEMAPHORE)then

    alias semID R3;

    if([SEMAPHORE_TABLE + semID*4] == currPID)then
        [SEMAPHORE_TABLE + semID*4] = -1;

        alias counter R5;
        counter = 0;
        while(counter < 16)do
            if([PROCESS_TABLE + counter*16 + 4] == WAIT_SEMAPHORE && [PROCESS_TABLE + counter*16 + 5] == semID)then
                [PROCESS_TABLE + counter*16 + 4] = READY;
                [PROCESS_TABLE + counter*16 + 5] = -1;
            endif;
            counter = counter + 1;
        endwhile;

        [SEMAPHORE_TABLE + semID*4 + 1] = [SEMAPHORE_TABLE + semID*4 + 1] - 1;

    endif;
    return;
endif;

if(funNum == ACQUIRE_INODE)then

    alias inodeIndex R3;

    while([FILE_STATUS_TABLE + inodeIndex*4] != -1)do
        
        [PROCESS_TABLE + currPID*16 + 4] = WAIT_FILE;
        [PROCESS_TABLE + currPID*16 + 5] = inodeIndex;

        multipush(R1, R2, R3, R4);
        call MOD_5;
        multipop(R1, R2, R3, R4);

    endwhile;

    if([INODE_TABLE + inodeIndex*16 + 1] == -1)then
        R0 = -1;
        return;
    endif;

    [FILE_STATUS_TABLE + inodeIndex*4] = currPID;
    R0 = 0;
    
    return;
endif;

if(funNum == RELEASE_INODE)then
    alias inodeIndex R3;

    if([FILE_STATUS_TABLE + inodeIndex*4] != currPID)then
        R0 = -1;
    else

        [FILE_STATUS_TABLE + inodeIndex*4] = -1;

        alias counter R5;
        counter = 0;
        while(counter < 16)do
            if([PROCESS_TABLE + counter*16 + 4] == WAIT_FILE && [PROCESS_TABLE + counter*16 + 5] == inodeIndex)then
                [PROCESS_TABLE + counter*16 + 4] = READY;
                [PROCESS_TABLE + counter*16 + 5] = -1;
            endif;
            counter = counter + 1;
        endwhile;

        R0 = 0;
    endif;

    return;
endif;

if(funNum == RELEASE_BUFFER)then

    alias buffNum R3;

    if(currPID != [BUFFER_TABLE + buffNum*4 + 2])then
        R0 = -1;
        return;
    endif;

    [BUFFER_TABLE + buffNum*4 + 2] = -1;

    alias counter R5;
    counter = 0;
    while(counter<16)do
        if([PROCESS_TABLE + counter*16 + 4] == WAIT_BUFFER && [PROCESS_TABLE + counter*16 + 5] == buffNum)then
            [PROCESS_TABLE + counter*16 + 4] = READY;
            [PROCESS_TABLE + counter*16 + 5] = -1;
        endif;
        counter = counter + 1;
    endwhile;

    R0 = 0;
    return;
endif;


if(funNum == ACQUIRE_BUFFER)then
    alias buffNum R3;

    while([BUFFER_TABLE + buffNum*4 + 2] != -1)do

        [PROCESS_TABLE + currPID*16 + 4] = WAIT_BUFFER;
        [PROCESS_TABLE + currPID*16 + 5] = buffNum;

        multipush(R1, R2, R3);
        call MOD_5;
        multipop(R1, R2, R3);
    endwhile;

    [BUFFER_TABLE + buffNum*4 + 2] = currPID;    

    return;
endif;
return;